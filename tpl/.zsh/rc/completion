autoload -U compinit
compinit
zmodload -i zsh/complist

setopt always_to_end            # when completing from the middle of a word, move the cursor to the end of the word
setopt complete_aliases         # complete alisases
setopt complete_in_word         # allow completion from within a word/phrase
setopt correct                  # spelling correction for commands
setopt hash_list_all            # hash everything before completion
setopt list_ambiguous           # complete as much of a completion until it gets ambiguous.
unsetopt list_beep              # beep on ambiguous completion
setopt menu_complete            #

zstyle ':completion::complete:*' use-cache on                                   # completion caching, use rehash to clear
zstyle ':completion:*' cache-path "${ZSH:-$HOME}/cache/ "                       # cache path
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'                       # ignore case
zstyle ':completion:*' menu select=2                                            #
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}                           # colorz !
zstyle ':completion:*::::' completer _expand _complete _ignored _approximate    # list of completers to use
zstyle :compinstall filename ~/.zshrc

# sections completion !
zstyle ':completion:*' verbose yes
zstyle ':completion:*:descriptions' format $'\e[00;34m%d'
zstyle ':completion:*:messages' format $'\e[00;31m%d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:manuals' separate-sections true

zstyle ':completion:*:processes' command 'ps -au$USER'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=29=34"
zstyle ':completion:*:*:killall:*' menu yes select
zstyle ':completion:*:killall:*' force-list always

# use /etc/hosts and known_hosts for hostname completion
[[ -r /etc/ssh/ssh_known_hosts ]] && _global_ssh_hosts=(${${${${(f)"$(</etc/ssh/ssh_known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _global_ssh_hosts=()
[[ -r ~/.ssh/known_hosts ]] && _ssh_hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _ssh_hosts=()
[[ -r /etc/hosts ]] && : ${(A)_etc_hosts:=${(s: :)${(ps:\t:)${${(f)~~"$(</etc/hosts)"}%%\#*}##[:blank:]#[^[:blank:]]#}}} || _etc_hosts=()
hosts=(
  "$_global_ssh_hosts[@]"
  "$_ssh_hosts[@]"
  "$_etc_hosts[@]"
  "$HOST"
  localhost
)
zstyle ':completion:*:hosts' hosts ${hosts}

# Don't complete uninteresting users
zstyle ':completion:*:*:*:users' ignored-patterns \
        adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
        dbus distcache dovecot fax ftp games gdm gkrellmd gopher \
        hacluster haldaemon halt hsqldb ident junkbust ldap lp mail \
        mailman mailnull mldonkey mysql nagios \
        named netdump news nfsnobody nobody nscd ntp nut nx openvpn \
        operator pcap postfix postgres privoxy pulse pvm quagga radvd \
        rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs
